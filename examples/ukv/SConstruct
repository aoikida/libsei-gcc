# -*- python -*-

# ----------------------------------------------------------------------
# Copyright (c) 2013 Diogo Behrens
# Distributed under the MIT license. See accompanying file LICENSE.
# ----------------------------------------------------------------------

env = Environment()
env.Append(CFLAGS = '-g -O0 ')

# basic ukv
ukv = env.Object(['ukv.c',
                  'hashtable/hashtable.c',
                  'hashtable/hashtable_utility.c',
                  'hashtable/hashtable_itr.c'])

# ukv interface test
env.Program(['ukv_test.c'] + ukv, LIBS = ['m'])

# ukv network interface (via messages)
ukv_net = env.Object('ukv_net.c')

# ukv network interface test
env.Program(['ukv_net_test.c'] + ukv + ukv_net, LIBS = ['m'])

# ukv server (telnet-like)
env.Program('ukv-server', ['ukv_server.c'] + ukv + ukv_net, LIBS = ['m'])

# ukv server with tmasco #######
env = env.Clone()
env.Append(LIBPATH=['../../build'])
env.Append(CPPPATH=['../../include'])

llvm = False
#llvm = True

if llvm:
    env.Replace(CC = 'clang')
    env.Append(CFLAGS = '-emit-llvm ')
    env.Append(CFLAGS = '-ftm ')
    env.Replace(OBJSUFFIX = '.bc')
else:
    env.Append(CFLAGS = '-fgnu-tm ')
env.Append(CFLAGS = '-DNDEBUG ')

# ukv server (telnet-like) with tmasco
env.Program('ukv-server.tmasco', ['tmasco_glue.c'], LIBS = ['tmasco', 'm'])
